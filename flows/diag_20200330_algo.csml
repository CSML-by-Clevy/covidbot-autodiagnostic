start:
	if (!responses_20200330) goto flow responses_20200330

	//////////////////////////////////////////////////////////////
	// Initialisation et calculs des variables ///////////////////
	//////////////////////////////////////////////////////////////
	remember algo_version = "2020-03-30"
	remember form_version = "2020-04-08"
	if (starting_ts && ending_ts) {
		remember date = starting_ts.iso
		remember duration = ending_ts.now - starting_ts.now
	}

	// Calcul des variables de l'algo
	// -- age range
	if (age < 15) remember age_range = "inf_15"
	else if (age >= 15 && age <= 49) remember age_range = "from_15_to_49"
	else if (age >= 50 && age <= 69) remember age_range = "from_50_to_69"
	else if (age >= 70) remember age_range = "sup_70"
	else remember age_range = "N/A"
	// -- température
	// -- -- calcul de la categorie
	if (temp < 35.5) remember temperature_cat = "inf_35.5"
	else if (temp >= 35.5 && temp <= 37.7) remember temperature_cat = "35.5-37.7"
	else if (temp > 37.7 && temp < 39) remember temperature_cat = "37.8-38.9"
	else if (temp >= 39) remember temperature_cat = "sup_39"
	else remember temperature_cat = "NSP"
	// -- -- calcul fievre
	if (fever == 999 || (fever == 1 && (temperature_cat == "inf_35.5" || temperature_cat == "sup_39" || temperature_cat == "NSP"))) {
		remember fever_algo = true
	} else remember fever_algo = false
	// -- IMC
	do sizem = size.to_float() / 100
	do sizem2 = sizem.pow(2)
	remember imc = weight.to_float() / sizem2
	// -- Maladie cardiaque (algo)
	if (heart_disease == 1 || heart_disease == 999) remember heart_disease_algo = true
	else remember heart_disease_algo = false
	// -- Maladie défenses immunitaires (algo)
	if (immunosuppressant_disease == 1) remember immunosuppressant_disease_algo = true
	else remember immunosuppressant_disease_algo = false
	// -- Traitement immunodépresseur (algo)
	if (immunosuppressant_drug == 1) remember immunosuppressant_drug_algo = true
	else remember immunosuppressant_drug_algo = false

	// Facteurs de gravité mineurs
	do fgmineurs = 0
	if (temp >= 39) do fgmineurs = fgmineurs + 1
	if (temp <= 35.4 && temp > 0) do fgmineurs = fgmineurs + 1
	if (tiredness_details == true) do fgmineurs = fgmineurs + 1

	// Facteurs de gravité majeurs
	do fgmajeurs = 0
	if (breathlessness == true) do fgmajeurs = fgmajeurs + 1
	if (feeding_day == true) do fgmajeurs = fgmajeurs + 1

	do hasNoPronosticFactors = (imc <= 30 &&
		heart_disease == false &&
		diabetes == false &&
		cancer == false &&
		breathing_disease == false &&
		kidney_disease == false &&
		liver_disease == false &&
		pregnant != 1 &&
		immunosuppressant_disease != true &&
		immunosuppressant_drug != true)

//////////////////////////////////////////////////////////////
// CALCULS DES NIVEAUX DE SYMPTOMES //////////////////////////
//////////////////////////////////////////////////////////////

	// Patients avec aucun symptome
	if (fever_algo == false &&
		cough == false &&
		sore_throat_aches == false &&
		diarrhea == false &&
		tiredness == false &&
		feeding_day == false &&
		breathlessness == false &&
		agueusia_anosmia == false
	) do symptomsLevel = 0

	// Tout patient sans fièvre avec un seul symptôme parmi toux, douleurs, anosmie
	if (fever_algo == false &&
		(cough == true && sore_throat_aches == false && agueusia_anosmia == false) ||
		(cough == false && sore_throat_aches == true && agueusia_anosmia == false) ||
		(cough == false && sore_throat_aches == false && agueusia_anosmia == true)) {
		do symptomsLevel = 1
	}

	// Tout patient avec
	// - fièvre
	// - ou sans fièvre et avec
	// ---- diarrhée
	// ---- ou toux et douleurs
	// ---- ou toux et anosmie
	if (fever_algo == true ||
		(cough == true && sore_throat_aches == true) ||
		(cough == true && agueusia_anosmia == true) ||
		(diarrhea == true)
	) do symptomsLevel = 2

	// Tout patient avec fièvre et toux :
	if (temp > 37.7 && cough == true) do symptomsLevel = 3



//////////////////////////////////////////////////////////////
// CALCULS CAS UNIQUES ///////////////////////////////////////
// SELON SYMPTOMES & FACTEURS GRAVITÉ ////////////////////////
//////////////////////////////////////////////////////////////

	if (age < 15) {
		remember orientation = responses_20200330.FIN1.name
		foreach (message) in responses_20200330.FIN1.content {
			say message
		}
		goto final
	}

	// Tout patient avec au moins un facteur de gravité majeur
	if (fgmajeurs > 0) {
		remember orientation = responses_20200330.FIN5.name
		foreach (message) in responses_20200330.FIN5.content {
			say message
		}
		goto final

	}

	// Tout patient avec fièvre et toux :
	if (symptomsLevel == 3) {
		// Tout patient sans facteur pronostique
		if (hasNoPronosticFactors == true) {
			remember orientation = responses_20200330.FIN6.name
			foreach (message) in responses_20200330.FIN6.content {
				say message
			}
			goto final
		}
		// Tout patient avec un facteur pronostique ou plus
		if (hasNoPronosticFactors == false) {
			// Si un ou deux facteurs de gravité mineurs
			if (fgmineurs <= 1) {
				remember orientation = responses_20200330.FIN6.name
				foreach (message) in responses_20200330.FIN6.content {
					say message
				}
				goto final
			}
			// Avec au moins deux facteurs de gravité mineurs
			if (fgmineurs > 1) {
				remember orientation = responses_20200330.FIN4.name
				foreach (message) in responses_20200330.FIN4.content {
					say message
				}
				goto final
			}
		}
	}

	// SI fièvre OU (pas de fièvre et (diarrhée OU (toux ET douleurs) OU (toux ET anosmie))
	if (symptomsLevel == 2) {
		// Tout patient sans facteur pronostique
		if (hasNoPronosticFactors == true) {
			// Si pas de facteur de gravité mineur
			if (fgmineurs == 0) {
				// Si moins de 50 ans
				if (age < 50) {
					remember orientation = responses_20200330.FIN2.name
					foreach (message) in responses_20200330.FIN2.content {
						say message
					}
					goto final
				} else {
					remember orientation = responses_20200330.FIN3.name
					foreach (message) in responses_20200330.FIN3.content {
						say message
					}
					goto final
				}
			}
			// Sinon (1 ou plusieurs facteurs de gravité mineurs)
			else if (fgmineurs >= 1) {
				remember orientation = responses_20200330.FIN3.name
				foreach (message) in responses_20200330.FIN3.content {
					say message
				}
				goto final
			}
		}
		// Tout patient avec un facteur pronostique ou plus
		if (hasNoPronosticFactors == false) {
			// Si zéro ou un facteur de gravité mineur
			if (fgmineurs < 2) {
				remember orientation = responses_20200330.FIN3.name
				foreach (message) in responses_20200330.FIN3.content {
					say message
				}
				goto final
			}
			// Au moins deux facteurs de gravité mineurs
			if (fgmineurs >= 2) {
				remember orientation = responses_20200330.FIN4.name
				foreach (message) in responses_20200330.FIN4.content {
					say message
				}
				goto final
			}
		}
	}

	// Tout patient sans fièvre avec un seul symptôme parmi toux, douleurs, anosmie
	if (symptomsLevel == 1) {
		// Au moins un facteur pronostique
		if (hasNoPronosticFactors == true) {
			remember orientation = responses_20200330.FIN2.name
			foreach (message) in responses_20200330.FIN2.content {
				say message
			}
			goto final
		} else {
			remember orientation = responses_20200330.FIN7.name
			foreach (message) in responses_20200330.FIN7.content {
				say message
			}
			goto final
		}
	}

	// Patients avec aucun symptome
	if (symptomsLevel == 0) {
		remember orientation = responses_20200330.FIN8.name
		foreach (message) in responses_20200330.FIN8.content {
			say message
		}
		goto final
	}

	// Cette phrase est appelée lorsque le cas n'est pas couvert par l'algorithme
	foreach (message) in responses_20200330.ERR.content {
		say message
	}
	goto final

final:
	// say "Restez chez vous au maximum en attendant que les symptômes disparaissent. Prenez votre température deux fois par jour. Rappel des mesures d’hygiène."
  foreach (message) in responses_20200330.FINAL.content {
		say message
	}

	// say "algo_version : {{algo_version}}"
	// say "form_version : {{form_version}}"
	// say "date : {{date}}"
	// say "orientation : {{orientation}}"
	// say "duration : {{duration}}"
	// say "imc : {{imc}}"
	// say "temperature_cat : {{temperature_cat}}"
	// say "fever_algo : {{fever_algo}}"
	// say "heart_disease_algo : {{heart_disease_algo}}"
	// say "immunosuppressant_drug_algo : {{immunosuppressant_drug_algo}}"
	// say "fever : {{fever}}"
	// say "temp : {{temp}}"
	// say "cough : {{cough}}"
	// say "agueusia_anosmia : {{agueusia_anosmia}}"
	// say "sore_throat_aches : {{sore_throat_aches}}"
	// say "diarrhea : {{diarrhea}}"
	// say "tiredness : {{tiredness}}"
	// say "tiredness_details : {{tiredness_details}}"
	// say "feeding_day : {{feeding_day}}"
	// say "breathlessness : {{breathlessness}}"
	// say "age : {{age}}"
	// say "size : {{size}}"
	// say "weight : {{weight}}"
	// say "heart_disease : {{heart_disease}}"
	// say "diabetes : {{diabetes}}"
	// say "cancer : {{cancer}}"
	// say "breathing_disease : {{breathing_disease}}"
	// say "kidney_disease : {{kidney_disease}}"
	// say "liver_disease : {{liver_disease}}"
	// say "pregnant : {{pregnant}}"
	// say "immunosuppressant_disease : {{immunosuppressant_disease}}"
	// say "immunosuppressant_drug : {{immunosuppressant_drug}}"
	// say "postal_code : {{postal_code}}"

	goto end
