start:
	//////////////////////////////////////////////////////////////
	// Initialisation et calculs des variables ///////////////////
	//////////////////////////////////////////////////////////////
	remember algo_version = "2020-03-30"
	remember form_version = "2020-04-08"
	// TODO remember date = "date ISO 8601	Ex. 2020-04-02T05:24:57.711-00:00"
	// TODO remember duration = end - start (int)

	// Calcul des variables de l'algo
	// -- age range
	if (age < 15) remember age_range = "inf_15"
	else if (age >= 15 && age <= 49) remember age_range = "from_15_to_49"
	else if (age >= 50 && age <= 69) remember age_range = "from_50_to_69"
	else if (age >= 70) remember age_range = "sup_70"
	else age_range = "N/A"
	// -- température
	// -- -- calcul de la categorie
	if (temp < 35.5) remember temperature_cat = "inf_35.5"
	else if (temp >= 35.5 && temp <= 37.7) remember temperature_cat = "35.5-37.7"
	else if (temp > 37.7 && temp < 39) remember temperature_cat = "37.8-38.9"
	else if (temp >= 39) remember temperature_cat = "sup_39"
	else temperature_cat = "NSP"
	// -- -- calcul fievre
	if (fever == 999 || (fever == 1 && (temperature_cat == "inf_35.5" || temperature_cat == "sup_39" || temperature_cat == "NSP"))) {
		remember fever_algo = true
	} else remember fever_algo = false
	// -- IMC
	do sizem = size.to_float() / 100
	do sizem2 = sizem.pow(2)
	remember imc = weight.to_float() / sizem2
	// -- Maladie cardiaque (algo)
	if (heart_disease == 1 || heart_disease == 999) remember heart_disease_algo = true
	else remember heart_disease_algo = false
	// -- Maladie défenses immunitaires (algo)
	if (immunosuppressant_disease == 1) remember immunosuppressant_disease_algo = true
	else remember immunosuppressant_disease_algo = false
	// -- Traitement immunodépresseur (algo)
	if (immunosuppressant_drug == 1) remember immunosuppressant_drug_algo = true
	else remember immunosuppressant_drug_algo = false

	// Facteurs de gravité mineurs
	do fgmineurs = 0
	if (temp >= 39) do fgmineurs = fgmineurs + 1
	if (temp <= 35.4 && temp > 0) do fgmineurs = fgmineurs + 1
	if (tiredness_details == 1) do fgmineurs = fgmineurs + 1

	// Facteurs de gravité majeurs
	do fgmajeurs = 0
	if (breathlessness == 1) do fgmajeurs = fgmajeurs + 1
	if (feeding_day == 1) do fgmajeurs = fgmajeurs + 1

	do hasNoPronosticFactors = (imc <= 30 &&
		heart_disease == 0 &&
		diabetes == 0 &&
		cancer == 0 &&
		breathing_disease == 0 &&
		kidney_disease == 0 &&
		liver_disease == 0 &&
		pregnant != 1 &&
		immunosuppressant_disease != 1 &&
		immunosuppressant_drug != 1)

//////////////////////////////////////////////////////////////
// CALCULS DES NIVEAUX DE SYMPTOMES //////////////////////////
//////////////////////////////////////////////////////////////

	// Patients avec aucun symptome
	if ((fever == 0 || temp == 0 || (temp >= 35.5 && temp <= 37.7)) &&
		cough == 0 &&
		sore_throat_aches == 0 &&
		diarrhea == 0 &&
		tiredness == 0 &&
		feeding_day == 0 &&
		breathlessness == 0
	) do symptomsLevel = 0

	// Tout patient sans fièvre avec un seul symptôme parmi toux, douleurs, anosmie
	if (cough + sore_throat_aches + agueusia_anosmia == 1 && temp >= 35.5 && temp <= 37.7) {
		do symptomsLevel = 1
	}

	// Tout patient avec
	// - fièvre
	// - ou sans fièvre et avec
	// ---- diarrhée
	// ---- ou toux et douleurs
	// ---- ou toux et anosmie
	if (temp > 37.7 ||
		(cough == 1 && sore_throat_aches == 1) ||
		(cough == 1 && agueusia_anosmia == 1) ||
		(diarrhea == 1)
	) do symptomsLevel = 2

	// Tout patient avec fièvre et toux :
	if (temp > 37.7 && cough == 1) do symptomsLevel = 3



//////////////////////////////////////////////////////////////
// CALCULS CAS UNIQUES ///////////////////////////////////////
// SELON SYMPTOMES & FACTEURS GRAVITÉ ////////////////////////
//////////////////////////////////////////////////////////////

	if (age < 15) {
		// FIN 1
		goto end
	}

	// Tout patient avec au moins un facteur de gravité majeur
	if (fgmajeurs > 0) {
		// FIN 5
		goto final

	}

	if (symptomsLevel == 0) {
		// FIN 8
		goto final
	}

	if (symptomsLevel == 1) {
		// Au moins un facteur pronostique
		if (hasNoPronosticFactors == true) {
			// FIN 2
			goto final
		} else {
			// FIN 7
			goto final
		}
	}

	if (symptomsLevel == 3) {
		// Tout patient sans facteur pronostique
		if (hasNoPronosticFactors == true) {
			// FIN 6
			goto final
		}
		// Tout patient avec un facteur pronostique ou plus
		if (hasNoPronosticFactors == false) {
			// Si un ou deux facteurs de gravité mineurs
			if (fgmineurs <= 1) {
				// FIN 6
				goto final
			}
			// Avec au moins deux facteurs de gravité mineurs
			if (fgmineurs > 1) {
				// FIN 4
				goto final
			}
		}
	}
	if (symptomsLevel == 2) {
		// Tout patient sans facteur pronostique
		if (hasNoPronosticFactors == true) {
			// Si pas de facteur de gravité mineur
			if (fgmineurs == 0) {
				// Si moins de 50 ans
				if (age < 50) {
					// FIN 2
					goto final
				} else {
					// FIN 3
					goto final
				}
			}
			// Sinon (1 ou plusieurs facteurs de gravité mineurs)
			else if (fgmineurs >= 1) {
				// FIN 3
				goto final
			}
		}
		// Tout patient avec un facteur pronostique ou plus
		if (hasNoPronosticFactors == false) {
			// Si zéro ou un facteur de gravité mineur
			if (fgmineurs < 2) {
				// FIN 3
				goto final
			}
			// Au moins deux facteurs de gravité mineurs
			if (fgmineurs >= 2) {
				// FIN 4
				goto final
			}
		}
	}
	//Cette phrase est appelée lorsque le cas n'est pas couvert par l'algorithme
	say "Malheureusement, *nous n'avons pas assez de donnée pour vous réaliser un autodiagnostic*. Prenez contact avec votre médecin généraliste au moindre doute 👨‍⚕️👩‍⚕️. En cas d’urgence, appeler le 15. 📞"
	goto final

final:
	//say "Restez chez vous au maximum en attendant que les symptômes disparaissent. Prenez votre température deux fois par jour. Rappel des mesures d’hygiène."
  say Typing(2000)
  say "Pour vous protéger et protéger les autres, dans la mesure du possible, *restez chez vous* et *limitez les contacts avec d'autres personnes.*"
  say Typing(2000)
  say Url("https://covid19-medicaments.com",
    text="Pour vérifier si un médicament pourrait présenter un risque potentiel d'aggraver vos symptômes")
  say Url("https://www.gouvernement.fr/info-coronavirus",
    text="Vous avez des questions additionnelles, appelez le 0800 130 000, ou suivez l'actualité sur : ")

	goto end
